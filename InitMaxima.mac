/*
Init File for school used it in this way:
  - place File in same directory as your Maxima File
  - load it with: batchload("filename")$
*/

kill(all)$
ratprint: false$
realonly:true$
fpprintprec: 4$
debug:false$
keepfloat:true$
(setf *debugger-hook* nil)$
load(draw)$

/* --------------- Hilfsfunktionen------------------------------------ */

ln(x):=float(log(x)/log(%e))$
lg(x):=float(log(x)/log(10))$


/* --------------- Zeichnen ----------------------------------------- */
/* testet ob es eine Variable gibt */
check_Var(test, options):=block(
    /* make local variables */
    [a],
    a:false,
     /* Key vorhanden, verschieden von false ? */
    if assoc(test,options ) # false then a:true,
    return(a)
)$

/* private function */
plotall(vvon,vbis,vy,vxlabel,vylabel,vtitle,vpointlist,vfilename,vtL,vxtics):=block(
  wxdraw2d(
    xrange=[vvon,vbis],
    yrange=vy,
    xaxis=true, yaxis=true,
    xlabel=vxlabel,
    ylabel=vylabel,
    title=vtitle,
    vtL,
    xtics=vxtics,
    vpointlist
 ),

 /* Ausgabe Datei */
 if vfilename # "-1" then
      draw2d(
        xrange=[vvon,vbis],
        yrange=vy,
        xaxis=true, yaxis=true,
        xlabel=vxlabel,
        ylabel=vylabel,
        title=vtitle,
        grid=true,
        xtics=vxtics,
        vtL,
        vpointlist,
        user_preamble = "set encoding utf8; set terminal svg enhanced font \"Helvetica,18\"",
        file_name = vfilename,
        terminal = svg
      )
)$
/*----------------------------------------------------------------------------*/
check_Labels(options):=block(
  /* lokale Variable */
  [vlabels,vtL],

  /* Beschriftungen im Stil [ ["Name",x,y], ... ] */
  if check_Var(labels, options)=false then  vlabels: []
  else                                      vlabels: assoc(labels,options),
  vtL:[],
  /* jeden Label Eintrag eigens als label() angeben mit Farbe black */
  for i: 1 while  i<=length(vlabels) do block(
    push(label(vlabels[i]), vtL),
    push(color=black, vtL)
  ),
  return(vtL)
)$

/* Liste von [Text, xWert] */
check_xTics(options):=block(
  [vxtics],
  if check_Var(xtics, options)=false then  vxtics: true
  else                                     vxtics: assoc(xtics,options),
  return(vxtics)
)$
/*----------------------------------------------------------------------------*/
/* Liste von Funktionen oder nur eine Funktion? */
check_Gl(gl):=block(
  /* lokale Variable */
  [vgl],

  /* Liste? */
  if listp(gl) then block(
    if length(gl)=1 then vgl: makelist(gl)
    else vgl: gl
  ) else vgl: makelist(gl),
  return(vgl)
 )$
/*----------------------------------------------------------------------------*/
check_Colors(gl, options):=block(
 /* lokale Variable */
 [vcolors,blacklist],
 if check_Var(colors, options)=false then block(
   /* alle Farben sw machen */
   if listp(gl) then vcolors: makelist (black, i, length(gl))
   else              vcolors: makelist (black, i, 1)
 )
 else block(
   vcolors: assoc(colors,options),
   /* bis zum Ende schwarz machen */
   if(length(vcolors)<length(gl)) then block(
     blacklist: makelist ("black", i, 1, length(gl)-length(vcolors)),
     vcolors: append(vcolors, blacklist)
   )
 ),
 return(vcolors)
)$
/*----------------------------------------------------------------------------*/
check_Width(gl, options):=block(
 /* lokale Variable */
 [vwidth,blacklist],

 if check_Var(width, options)=false then block(
   /* alle Linien Stärken 3 machen */
   if listp(gl) then vwidth: makelist (3, i, length(gl))
   else              vwidth: makelist (3, i, 1)
 )
 else block(
   vwidth: assoc(width,options),
   /* bis zum Ende width 3 machen */
   if(length(vwidth)<length(gl)) then block(
     blacklist: makelist (3, i, 1, length(gl)-length(vwidth)),
     vwidth: append(vwidth, blacklist)
   )
 ),
 return(vwidth)
)$
/*----------------------------------------------------------------------------*/
check_Subst(gl, options):=block(
 /* lokale Variable */
 [v],

 if check_Var(subst, options)=false then v: []
 else v: assoc(subst,options),
 return(v)
)$
/*----------------------------------------------------------------------------*/
check_Legend(gl, options):=block(
  /* lokale Variable */
  [vL,blacklist],

  /* Legende setzen */
  if check_Var(legend, options)=false then block(
    /* keine Legende alles leer machen */
    if listp(gl) then vL: makelist ("", i, 1, length(gl))
    else              vL: makelist ("", i, 1, 1)
  )
  else block(
    vL: assoc(legend,options),
    /* bis zum Ende leer machen */
    if(length(vL)<length(gl)) then block(
      blacklist: makelist ("", i, 1, length(gl)-length(vL)),
      vL: append(vL, blacklist)
    )
  ),
  return(vL)
)$
/*----------------------------------------------------------------------------*/
do_Subst(vsubst, vgl):=block(
  /* Alle Ersetzungen durchführen */
  /* 2er Schritte a,b  dann c,d */
  for i: 1 step 2 thru length(vsubst) do block(
    for k: 1 while  k<=length(vgl) do block(
      if(i+1<=length(vsubst)) then
        vgl[k]: subst(vsubst[i+1], vsubst[i], vgl[k])
    )
  ),
  return(vgl)
)$
/*----------------------------------------------------------------------------*/
/* liegen einzelne Punkte vor? */
/* können auch Rechtecke sein */
is_PointList(vgl):=block(
  [ispointlist],
  ispointlist:false,

  /* Einzelfkt */
  if(listp(vgl)) then block(
      for i: 1 while  i<=length(vgl) do block(
      /* teste Element ob Punktkoordinaten vorliegen*/
      if listp(vgl[i]) then block(
        /* Pkt haben x,y Koord=Liste */
        /* entweder EinzelpunktListe oder Liste von Punktlisten */
        if length(vgl[i])=2 or length(first(vgl[i]))=2 then ispointlist:true
      )
    )
  ),
  return(ispointlist)
)$

/*----------------------------------------------------------------------------*/

Zeichne(gl,von,bis,[options]):=
block(
    /* lokale Variable */
    [vfilename,vxlabel,vylabel,vy,vtitle,vcolors,vtL,vlegend,vL,vgl,vsubst,isptlist,vbars, vparametric, vxtics],

    /* Optionale Parameter laden bzw. setzen */
    if check_Var(filename, options)=false then vfilename:"-1"
    else                                       vfilename: assoc(filename,options),
    if check_Var(xlabel, options)=false then vxlabel:"x-Achse"
    else                                     vxlabel: assoc(xlabel,options),

    if check_Var(ylabel, options)=false then vylabel:"y-Achse"
    else                                     vylabel: assoc(ylabel,options),

    if check_Var(title, options)=false then  vtitle:""
    else                                     vtitle: assoc(title,options),

    if check_Var(titel, options)=false then  vtitle:""
    else                                     vtitle: assoc(titel,options),

    if check_Var(y, options)=false then  vy: auto
    else                                 vy: assoc(y,options),
    if check_Var(bars, options)=false then  vbars: 0
    else                                    vbars: 1,
    if check_Var(parametric, options)=false then  vparametric: 0
    else                                          vparametric: 1,
    /* check all Parameters --------------------------------------------------*/
    vgl: check_Gl(gl),
    vtL: check_Labels(options),
    vcolors: check_Colors(gl, options),
    vwidth: check_Width(gl, options),
    vL: check_Legend(gl, options),
    vsubst: check_Subst(gl, options),
    vgl: do_Subst(vsubst, vgl),
    vxtics: check_xTics(options),

    vpointlist:[],
    /* nur eine Punktliste mind 2 Punkte hintereinander, ansonsten mehrere PktListen oder Fkt */
    /* muss eine Liste sein */
    if listp(first(vgl)) and listp(second(vgl)) and length(first(vgl))=2 and length(second(vgl))=2 then block(
      vpointlist: append(vpointlist, [key=vL[1], color=vcolors[1], point_size=1.5,point_type=7,line_width=vwidth[1],points_joined=true, points(vgl)])
    )
    else
      for i: 1 while  i<=length(vgl) do block(
        /* Punktliste */
        if is_PointList(vgl[i]) then
          vpointlist: append(vpointlist, [key=vL[i], color=vcolors[i], point_size=1.5,point_type=7,line_width=vwidth[i],points_joined=true, points(vgl[i])])
        /* Funktion explizit */
        else block(
          if vbars=1 then
            vpointlist: append(vpointlist, [key=vL[i], line_width=vwidth[i],vgl[i]])
          else block(
            if vparametric=1 then block(
              /* Parametric Plot */
              vpointlist: append(vpointlist, [key=vL[i], color=vcolors[i], line_width=vwidth[i],vgl[i]])
            )else
              vpointlist: append(vpointlist, [key=vL[i], color=vcolors[i], line_width=vwidth[i],explicit(vgl[i],x,von,bis)])
          )
        )
      ),
      plotall(von, bis, vy, vxlabel, vylabel, vtitle, vpointlist, vfilename, vtL, vxtics)

)$

/* --------------- Funktion aufstellen ------------------------------- */
CreateFunktion(p1,p2, f):=
block(
    gl1:subst ([x=p1[1], y=p1[2]], f),
    gl2:subst ([x=p2[1], y=p2[2]], f),
    erg: solve([gl1,gl2],[a,b]),
    return(
        subst ([a=rhs(erg[1][1]), b=rhs(erg[1][2])], f)
    )
)$

/* --------------- Vektoren ---------------------------- */
getX(v):=block(
  if listp(v) and length(v)>0 then return(v[1])
  else error("Kann xKoordinate nicht ausgeben, ist das eine Vektor?")
)$

getY(v):=block(
  if listp(v) and length(v)>1 then return(v[2])
  else error("Kann yKoordinate nicht ausgeben, ist das eine Vektor?")
)$

getZ(v):=block(
  if listp(v) and length(v)>2 then return(v[3])
  else error("Kann zKoordinate nicht ausgeben, ist das eine Vektor?")
)$

/* --------------- Tools ---------------------------- */

toRad(α):=α*π/180$
toGrad(α):=α*180/π$
